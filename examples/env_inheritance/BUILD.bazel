load("@aspect_bazel_lib//lib:testing.bzl", "assert_contains")
load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//oci:defs.bzl", "oci_image")

pkg_tar(
    name = "app",
    srcs = ["test.bash"],
)

oci_image(
    name = "middle_image",
    base = "@ubuntu",
    cmd = ["test.sh"],
    env = {
        "ENV1": "$PATH:/test",
        "ENV2": "/prepend:${PATH}:/test2",
        "ENV3": "key1=value1 key2=value2",
    },
    tars = ["app.tar"],
)

oci_image(
    name = "image",
    base = ":middle_image",
    env = {
        "ENV4": "${ENV3} key3=value3",
    },
)

container_structure_test(
    name = "test",
    configs = ["test.yaml"],
    image = ":image",
)

genrule(
    name = "hash",
    srcs = [":image"],
    outs = ["sha256.sum"],
    cmd = "$(JQ_BIN) -r '.manifests[0].digest' $(location :image)/index.json > $@",
    toolchains = ["@jq_toolchains//:resolved_toolchain"],
)

assert_contains(
    name = "check_digest",
    actual = ":hash",
    expected = "sha256:d15c83f8f8438e5e05ff40478dc98ad923e9afa272b6630c5b29baf6f7e93585",
    # TODO: enable once there's a new version of crane
    # https://github.com/google/go-containerregistry/issues/1542
    # https://github.com/bazel-contrib/rules_oci/issues/58
    tags = ["manual"],
)
