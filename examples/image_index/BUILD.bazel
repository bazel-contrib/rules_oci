load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index")
load("@tar.bzl", "tar")

platform(
    name = "linux_arm64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:arm64",
    ],
)

platform(
    name = "linux_amd64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "platform_linux_arm64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:arm64",
    ],
)

config_setting(
    name = "platform_linux_amd64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

copy_file(
    name = "entrypoint_sh",
    src = select({
        ":platform_linux_arm64": "say_hello_arm64.sh",
        ":platform_linux_amd64": "say_hello_amd64.sh",
    }),
    out = "entrypoint.sh",
    target_compatible_with = [
        "@platforms//os:linux",
    ],
)

tar(
    name = "say_hello",
    srcs = [":entrypoint.sh"],
)

oci_image(
    name = "app",
    base = "@debian",
    cmd = ["/examples/image_index/entrypoint.sh"],
    tars = [":say_hello"],
)

oci_image_index(
    name = "app_all",
    images = [":app"],
    platforms = [
        ":linux_amd64",
        ":linux_arm64",
    ],
)
